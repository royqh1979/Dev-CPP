{-------------------------------------------------------------------------------
The contents of this file are subject to the Mozilla Public License
Version 1.1 (the "License"); you may not use this file except in compliance
with the License. You may obtain a copy of the License at
http://www.mozilla.org/MPL/

Software distributed under the License is distributed on an "AS IS" basis,
WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License for
the specific language governing rights and limitations under the License.

The Original Code is: SynHighlighterCpp.pas, released 2000-04-10.
The Original Code is based on the dcjCppSyn.pas file from the
mwEdit component suite by Martin Waldenburg and other developers, the Initial
Author of this file is Michael Trier.
All Rights Reserved.

Contributors to the SynEdit and mwEdit projects are listed in the
Contributors.txt file.

Alternatively, the contents of this file may be used under the terms of the
GNU General Public License Version 2 or later (the "GPL"), in which case
the provisions of the GPL are applicable instead of those above.
If you wish to allow use of your version of this file only under the terms
of the GPL and not to allow others to use your version of this file
under the MPL, indicate your decision by deleting the provisions above and
replace them with the notice and other provisions required by the GPL.
If you do not delete the provisions above, a recipient may use your version
of this file under either the MPL or the GPL.

$Id: SynHighlighterCpp.pas,v 1.24 2005/01/28 16:53:21 maelh Exp $

You may retrieve the latest version of this file at the SynEdit home page,
located at http://SynEdit.SourceForge.net

Known Issues:
-------------------------------------------------------------------------------}
{
@abstract(Provides a C++ syntax highlighter for SynEdit)
@author(Michael Trier)
@created(1998)
@lastmod(2001-11-21)
The SynHighlighterCpp unit provides SynEdit with a C++ syntax highlighter.
Thanks to Martin Waldenburg.
}

{$IFNDEF QSYNHIGHLIGHTERCPP}
unit SynHighlighterCpp;
{$ENDIF}

{$I SynEdit.inc}

interface

uses
  Graphics,
  SynEditTypes,
  SynEditHighlighter,
  SysUtils,
  Classes;

type
  TtkTokenKind = (tkAsm, tkComment, tkDirective, tkIdentifier, tkKey, tkNull,
    tkNumber, tkSpace, tkString, tkStringEscapeSeq, tkSymbol, tkUnknown,
    tkChar, tkFloat, tkHex, tkOctal,tkRawString);

  TxtkTokenKind = (
    xtkAdd, xtkAddAssign, xtkAnd, xtkAndAssign, xtkArrow, xtkAssign,
    xtkBitComplement, xtkBraceClose, xtkBraceOpen, xtkColon, xtkComma,
    xtkDecrement, xtkDivide, xtkDivideAssign, xtkEllipse, xtkGreaterThan,
    xtkGreaterThanEqual, xtkIncOr, xtkIncOrAssign, xtkIncrement, xtkLessThan,
    xtkLessThanEqual, xtkLogAnd, xtkLogComplement, xtkLogEqual, xtkLogOr,
    xtkMod, xtkModAssign, xtkMultiplyAssign, xtkNotEqual, xtkPoint, xtkQuestion,
    xtkRoundClose, xtkRoundOpen, xtkScopeResolution, xtkSemiColon, xtkShiftLeft,
    xtkShiftLeftAssign, xtkShiftRight, xtkShiftRightAssign, xtkSquareClose,
    xtkSquareOpen, xtkStar, xtkSubtract, xtkSubtractAssign, xtkXor,
    xtkXorAssign);

  {
    rsUnknow: all other characters don't need highlight
    rsDirective: C/CPP precompile directives like #include <iostream>
    rsDirectiveComment: ANSI-Style Comment embedded in Directive
    rsMultiLineDirective:  C/CPP precompile directives breaked to multiple lines
    rsAnsiC: ANSI-Style c comment like /* xxx */
    rsCppComment: cpp style comment like //xxx
    rsString: string like "xxx"
    rsMultiLineString: a string on multiline    like
        "xxxxx \
        xxxxx"
  }
  TRangeState = (rsUnknown, rsAnsiC, rsAnsiCAsm, rsAnsiCAsmBlock, rsAsm,
    rsAsmBlock, rsDirective, rsDirectiveComment, rsString,
    rsMultiLineString, rsMultiLineDirective, rsCppComment,
    rsStringEscapeSeq, rsMultiLineStringEscapeSeq,
    rsRawString, rsSpace,rsRawStringEscaping,rsRawStringNotEscaping,rsChar);

  TProcTableProc = procedure of object;

  PIdentFuncTableFunc = ^TIdentFuncTableFunc;
  TIdentFuncTableFunc = function: TtkTokenKind of object;

  TSynCppSyn = class(TSynCustomHighlighter)
  private
    fAsmStart: Boolean;
    fRange: TRangeState;
    fSpaceRange: TRangeState;
    fParenthesisLevel: integer;
    fBracketLevel: integer;
    fBraceLevel:integer;
    fLine: PChar;
    fProcTable: array[#0..#255] of TProcTableProc;
    Run: LongInt;
    fStringLen: Integer;
    fToIdent: PChar;
    fTokenPos: Integer;
    FTokenID: TtkTokenKind;
    FExtTokenID: TxtkTokenKind;
    fLineNumber: Integer;
    fAsmAttri: TSynHighlighterAttributes;
    fCommentAttri: TSynHighlighterAttributes;
    fDirecAttri: TSynHighlighterAttributes;
    fIdentifierAttri: TSynHighlighterAttributes;
    fInvalidAttri: TSynHighlighterAttributes;
    fKeyAttri: TSynHighlighterAttributes;
    fNumberAttri: TSynHighlighterAttributes;
    fFloatAttri: TSynHighlighterAttributes;
    fHexAttri: TSynHighlighterAttributes;
    fOctalAttri: TSynHighlighterAttributes;
    fSpaceAttri: TSynHighlighterAttributes;
    fStringAttri: TSynHighlighterAttributes;
    fStringEscapeSeqAttri: TSynHighlighterAttributes;
    fCharAttri: TSynHighlighterAttributes;
    fSymbolAttri: TSynHighlighterAttributes;
    fVariableAttri: TSynHighlighterAttributes;
    fFunctionAttri: TSynHighlighterAttributes;
    fClassAttri: TSynHighlighterAttributes;
    fGlobalVarAttri: TSynHighlighterAttributes;
    fLocalVarAttri : TSynHighlighterAttributes;
    procedure AnsiCProc;
    procedure AnsiCppProc;
    procedure AndSymbolProc;
    procedure AsciiCharProc;
    procedure AtSymbolProc;
    procedure BraceCloseProc;
    procedure BraceOpenProc;
    procedure CRProc;
    procedure ColonProc;
    procedure CommaProc;
    procedure DirectiveProc;
    procedure DirectiveEndProc;
    procedure EqualProc;
    procedure GreaterProc;
    procedure IdentProc;
    procedure LFProc;
    procedure LowerProc;
    procedure MinusProc;
    procedure ModSymbolProc;
    procedure NotSymbolProc;
    procedure NullProc;
    procedure NumberProc;
    procedure OrSymbolProc;
    procedure PlusProc;
    procedure PointProc;
    procedure QuestionProc;
    procedure RoundCloseProc;
    procedure RoundOpenProc;
    procedure SemiColonProc;
    procedure SlashProc;
    procedure SpaceProc;
    procedure SquareCloseProc;
    procedure SquareOpenProc;
    procedure StarProc;
    procedure StringProc;
    procedure TildeProc;
    procedure XOrSymbolProc;
    procedure UnknownProc;
    function IdentKind(MayBe: PChar): TtkTokenKind;
    procedure MakeMethodTables;
    procedure StringEndProc;
    procedure StringStartProc;
    procedure RawStringProc;
    procedure StringEscapeSeqProc;
  protected
    function GetIdentChars: TSynIdentChars; override;
    function GetExtTokenID: TxtkTokenKind;
    function GetSampleSource: string; override;
    function IsFilterStored: Boolean; override;
  public
    class function GetCapabilities: TSynHighlighterCapabilities; override;
    class function GetLanguageName: string; override;
  public
    constructor Create(AOwner: TComponent); override;
    function GetDefaultAttribute(Index: integer): TSynHighlighterAttributes;
      override;
    function GetEol: Boolean; override;
    function GetRange: Pointer; override;
    function GetBraceLevel: integer; override;
    function GetBracketLevel: integer; override;
    function GetParenthesisLevel: integer; override;
    function GetTokenID: TtkTokenKind;
    procedure SetLine(NewValue: String; LineNumber:Integer); override;
    function GetToken: String; override;
    function GetTokenAttribute: TSynHighlighterAttributes; override;
    function GetTokenKind: integer; override;
    function GetTokenPos: Integer; override;
    function GetTokenFinished: boolean; override;
    function GetIsLastLineCommentNotFinish(value:Pointer):boolean; override;
    function GetIsLastLineStringNotFinish(value:Pointer):boolean; override;
    function GetTokenType: TSynHighlighterTokenType; override;
    procedure Next; override;
    procedure SetRange(Value: Pointer); override;
    procedure SetParenthesisLevel(Value: integer); override;
    procedure SetBracketLevel(Value: integer); override;
    procedure SetBraceLevel(Value: integer); override;
    procedure ResetRange; override;
    procedure ResetParenthesisLevel; override;
    procedure ResetBracketLevel; override;
    procedure ResetBraceLevel; override;
    function UseUserSettings(settingIndex: integer): boolean; override;
    procedure EnumUserSettings(settings: TStrings); override;
    property ExtTokenID: TxtkTokenKind read GetExtTokenID;
  published
    property AsmAttri: TSynHighlighterAttributes read fAsmAttri write fAsmAttri;
    property CommentAttri: TSynHighlighterAttributes read fCommentAttri
      write fCommentAttri;
    property DirecAttri: TSynHighlighterAttributes read fDirecAttri
      write fDirecAttri;
    property IdentifierAttri: TSynHighlighterAttributes read fIdentifierAttri
      write fIdentifierAttri;
    property InvalidAttri: TSynHighlighterAttributes read fInvalidAttri
      write fInvalidAttri;
    property KeyAttri: TSynHighlighterAttributes read fKeyAttri write fKeyAttri;
    property NumberAttri: TSynHighlighterAttributes read fNumberAttri
      write fNumberAttri;
    property FloatAttri: TSynHighlighterAttributes read fFloatAttri
      write fFloatAttri;
    property HexAttri: TSynHighlighterAttributes read fHexAttri
      write fHexAttri;
    property OctalAttri: TSynHighlighterAttributes read fOctalAttri
      write fOctalAttri;
    property SpaceAttri: TSynHighlighterAttributes read fSpaceAttri
      write fSpaceAttri;
    property StringAttri: TSynHighlighterAttributes read fStringAttri
      write fStringAttri;
    property StringEscapeSeqAttri: TSynHighlighterAttributes read fStringEscapeSeqAttri
      write fStringEscapeSeqAttri;
    property CharAttri: TSynHighlighterAttributes read fCharAttri
      write fCharAttri;
    property SymbolAttri: TSynHighlighterAttributes read fSymbolAttri
      write fSymbolAttri;
    property VariableAttri: TSynHighlighterAttributes read fVariableAttri
      write fVariableAttri;
    property FunctionAttri: TSynHighlighterAttributes read fFunctionAttri
      write fFunctionAttri;
    property ClassAttri: TSynHighlighterAttributes read fClassAttri
      write fClassAttri;
    property GlobalVarAttri: TSynHighlighterAttributes read fGlobalVarAttri
      write fGlobalVarAttri;
    property LocalVarAttri: TSynHighlighterAttributes read fLocalVarAttri
      write fLocalVarAttri;

  end;



implementation

uses
  iniFiles,
  Windows,
  SynEditStrConst;

var
  Identifiers: array[#0..#255] of ByteBool;
  // mHashTable: array[#0..#255] of Integer;
  CppKeywords : TStringHash;

procedure MakeIdentTable;
var
  I: Char;
begin
  for I := #0 to #255 do
  begin
    Case I of
      '_', '0'..'9', 'a'..'z', 'A'..'Z', #128 .. #255 : Identifiers[I] := True;
    else Identifiers[I] := False;
    end;
    {
    Case I in['_', 'a'..'z', 'A'..'Z'] of
      True:
        begin
          if (I > #64) and (I < #91) then mHashTable[I] := Ord(I) - 64 else
            if (I > #96) then mHashTable[I] := Ord(I) - 95;
        end;
    else mHashTable[I] := 0;
    end;
    }
  end;
end;


function TSynCppSyn.IdentKind(MayBe: PChar): TtkTokenKind;
var
  ToHash : PChar;
  fStringLen : integer;
  Key:String;
begin
  fToIdent := MayBe; { things like 'int a;' }
  ToHash := fToIdent;
  while ToHash^ in ['_', '0'..'9', 'a'..'z', 'A'..'Z', #128..#255] do
  begin
    inc(ToHash);
  end;
  fStringLen := ToHash - fToIdent;
  SetString(Key, fToIdent, fStringLen); { we need to get 'int' }
  if CppKeywords.ValueOf(Key) = -1 then
    Result := tkIdentifier
  else
    Result := tkKey;
end;

procedure TSynCppSyn.MakeMethodTables;
var
  I: Char;
begin
  for I := #0 to #255 do
    case I of
      '&': fProcTable[I] := AndSymbolProc;
      #39: fProcTable[I] := AsciiCharProc;
      '@': fProcTable[I] := AtSymbolProc;
      '}': fProcTable[I] := BraceCloseProc;
      '{': fProcTable[I] := BraceOpenProc;
      #13: fProcTable[I] := CRProc;
      ':': fProcTable[I] := ColonProc;
      ',': fProcTable[I] := CommaProc;
      '#': fProcTable[I] := DirectiveProc;
      '=': fProcTable[I] := EqualProc;
      '>': fProcTable[I] := GreaterProc;
      '?': fProcTable[I] := QuestionProc;
      'A'..'Z', 'a'..'z', '_', #128..#255 : fProcTable[I] := IdentProc;
      #10: fProcTable[I] := LFProc;
      '<': fProcTable[I] := LowerProc;
      '-': fProcTable[I] := MinusProc;
      '%': fProcTable[I] := ModSymbolProc;
      '!': fProcTable[I] := NotSymbolProc;
      #0: fProcTable[I] := NullProc;
      '0'..'9': fProcTable[I] := NumberProc;
      '|': fProcTable[I] := OrSymbolProc;
      '+': fProcTable[I] := PlusProc;
      '.': fProcTable[I] := PointProc;
      ')': fProcTable[I] := RoundCloseProc;
      '(': fProcTable[I] := RoundOpenProc;
      ';': fProcTable[I] := SemiColonProc;
      '/': fProcTable[I] := SlashProc;
      #1..#9, #11, #12, #14..#32: fProcTable[I] := SpaceProc;
      ']': fProcTable[I] := SquareCloseProc;
      '[': fProcTable[I] := SquareOpenProc;
      '*': fProcTable[I] := StarProc;
      #34: fProcTable[I] := StringStartProc;
      '~': fProcTable[I] := TildeProc;
      '^': fProcTable[I] := XOrSymbolProc;
      else fProcTable[I] := UnknownProc;
    end;
end;

constructor TSynCppSyn.Create(AOwner: TComponent);
begin
  inherited Create(AOwner);
  fAsmAttri := TSynHighlighterAttributes.Create(SYNS_AttrAssembler);
  AddAttribute(fAsmAttri);
  fCharAttri := TSynHighlighterAttributes.Create(SYNS_AttrCharacter);
  AddAttribute(fCharAttri);
  fCommentAttri := TSynHighlighterAttributes.Create(SYNS_AttrComment);
  fCommentAttri.Style:= [fsItalic];
  AddAttribute(fCommentAttri);
  fClassAttri := TSynHighlighterAttributes.Create(SYNS_AttrClass);
  AddAttribute(fClassAttri);
  fFloatAttri := TSynHighlighterAttributes.Create(SYNS_AttrFloat);
  AddAttribute(fFloatAttri);
  fFunctionAttri := TSynHighlighterAttributes.Create(SYNS_AttrFunction);
  AddAttribute(fFunctionAttri);
  fGlobalVarAttri := TSynHighlighterAttributes.Create(SYNS_AttrGlobalVariable);
  AddAttribute(fGlobalVarAttri);
  fHexAttri := TSynHighlighterAttributes.Create(SYNS_AttrHexadecimal);
  AddAttribute(fHexAttri);
  fIdentifierAttri := TSynHighlighterAttributes.Create(SYNS_AttrIdentifier);
  AddAttribute(fIdentifierAttri);
  fInvalidAttri := TSynHighlighterAttributes.Create(SYNS_AttrIllegalChar);
  AddAttribute(fInvalidAttri);
  fLocalVarAttri := TSynHighlighterAttributes.Create(SYNS_AttrLocalVariable);
  AddAttribute(fLocalVarAttri);
  fNumberAttri := TSynHighlighterAttributes.Create(SYNS_AttrNumber);
  AddAttribute(fNumberAttri);
  fOctalAttri := TSynHighlighterAttributes.Create(SYNS_AttrOctal);
  AddAttribute(fOctalAttri);
  fDirecAttri := TSynHighlighterAttributes.Create(SYNS_AttrPreprocessor);
  AddAttribute(fDirecAttri);
  fKeyAttri := TSynHighlighterAttributes.Create(SYNS_AttrReservedWord);
  fKeyAttri.Style:= [fsBold];
  AddAttribute(fKeyAttri);
  fSpaceAttri := TSynHighlighterAttributes.Create(SYNS_AttrSpace);
  fSpaceAttri.Foreground := clWindow;
  AddAttribute(fSpaceAttri);
  fStringAttri := TSynHighlighterAttributes.Create(SYNS_AttrString);
  AddAttribute(fStringAttri);
  fStringEscapeSeqAttri := TSynHighlighterAttributes.Create(SYNS_AttrStringEscapeSequences);
  AddAttribute(fStringEscapeSeqAttri);
  fSymbolAttri := TSynHighlighterAttributes.Create(SYNS_AttrSymbol);
  AddAttribute(fSymbolAttri);
  fVariableAttri := TSynHighlighterAttributes.Create(SYNS_AttrVariable);
  AddAttribute(fVariableAttri);

  SetAttributesOnChange(DefHighlightChange);
  MakeMethodTables;
  fRange := rsUnknown;
  fSpaceRange := rsUnknown;
  fParenthesisLevel := 0;
  fBracketLevel := 0;
  fBraceLevel := 0;
  fAsmStart := False;
  fDefaultFilter := SYNS_FilterCPP;
end; { Create }

procedure TSynCppSyn.SetLine(NewValue: String; LineNumber:Integer);
begin
  fLine := PChar(NewValue);
  Run := 0;
  fSpaceRange := rsUnknown;
  fLineNumber := LineNumber;
  Next;
end; { SetLine }

procedure TSynCppSyn.AnsiCProc;
begin
  fTokenID := tkComment;
  case FLine[Run] of
    #0:
      begin
        NullProc;
        exit;
      end;
    #10:
      begin
        LFProc;
        exit;
      end;
    #13:
      begin
        CRProc;
        exit;
      end;
  end;

  while FLine[Run] <> #0 do
    case FLine[Run] of
      '*':
        if fLine[Run + 1] = '/' then
        begin
          inc(Run, 2);
          if fRange = rsAnsiCAsm then
            fRange := rsAsm
          else if fRange = rsAnsiCAsmBlock then
            fRange := rsAsmBlock
          else if (fRange = rsDirectiveComment)  and
            not (fLine[Run] in [#0, #13, #10]) then
              fRange := rsMultiLineDirective
          else
            fRange := rsUnKnown;
          break;
        end else
          inc(Run);
      #10: break;
      #13: break;
      #32,#9: begin
        fSpaceRange := fRange;
        fRange := rsSpace;
        break;
      end;
    else inc(Run);
  end;
end;

procedure TSynCppSyn.AnsiCppProc;
begin
  fTokenID := tkComment;
  if fLine[Run]=#0 then begin
    NullProc;
    if (Run<1) or (fLine[Run-1] <> '\') then
      fRange := rsUnknown;
    Exit;
  end;
  while not (fLine[Run] in [#13,#10,#0]) do begin
    if (fLine[Run] in [#32,#9]) then begin
      fSpaceRange := fRange;
      fRange := rsSpace;
      break;
    end;
    Inc(Run);
  end;
  fRange := rsUnknown;
  if (fLine[Run-1] = '\') and (fLine[Run] = #0) then
    fRange := rsCppComment; // continues on next line
end;

procedure TSynCppSyn.AndSymbolProc;
begin
  fTokenID := tkSymbol;
  case FLine[Run + 1] of
    '=':                               {and assign}
      begin
        inc(Run, 2);
        FExtTokenID := xtkAndAssign;
      end;
    '&':                               {logical and}
      begin
        inc(Run, 2);
        FExtTokenID := xtkLogAnd;
      end;
  else                                 {and}
    begin
      inc(Run);
      FExtTokenID := xtkAnd;
    end;
  end;
end;

procedure TSynCppSyn.AsciiCharProc;
begin
  fTokenID := tkChar;
  repeat
    if fLine[Run] in [#32,#9] then begin
      fSpaceRange := rsChar;
      fRange := rsSpace;
      Exit;
    end;
    if fLine[Run] = '\' then begin
      if fLine[Run + 1] in [#39, '\'] then
        inc(Run);
    end;
    inc(Run);
  until fLine[Run] in [#0, #10, #13, #39];
  if fLine[Run] = #39 then
    inc(Run);
  fRange:= rsUnknown;
end;

procedure TSynCppSyn.AtSymbolProc;
begin
  fTokenID := tkUnknown;
  inc(Run);
end;

procedure TSynCppSyn.BraceCloseProc;
begin
  inc(Run);
  fTokenId := tkSymbol;
  FExtTokenID := xtkBraceClose;
  if fRange = rsAsmBlock then fRange := rsUnknown;
  dec(fBraceLevel);
end;

procedure TSynCppSyn.BraceOpenProc;
begin
  inc(Run);
  fTokenId := tkSymbol;
  FExtTokenID := xtkBraceOpen;
  if fRange = rsAsm then
  begin
    fRange := rsAsmBlock;
    fAsmStart := True;
  end;
  inc(fBraceLevel);
end;

procedure TSynCppSyn.CRProc;
begin
  fTokenID := tkSpace;
  Inc(Run);
  if fLine[Run + 1] = #10 then Inc(Run);
end;

procedure TSynCppSyn.ColonProc;
begin
  fTokenID := tkSymbol;
  Case FLine[Run + 1] of
    ':':                               {scope resolution operator}
      begin
        inc(Run, 2);
        FExtTokenID := xtkScopeResolution;
      end;
  else                                 {colon}
    begin
      inc(Run);
      FExtTokenID := xtkColon;
    end;
  end;
end;

procedure TSynCppSyn.CommaProc;
begin
  inc(Run);
  fTokenID := tkSymbol;
  FExtTokenID := xtkComma;
end;

procedure TSynCppSyn.DirectiveProc;
begin
  if Trim(fLine)[1] <> '#' then // '#' is not first char on the line, treat it as an invalid char
  begin
    fTokenID := tkUnknown;
    Inc(Run);
    Exit;
  end;
  fTokenID := tkDirective;
  repeat
    if fLine[Run] in [#9,#32] then begin
      fSpaceRange := rsMultiLineDirective;
      fRange := rsSpace;
      Exit;
    end;
    if fLine[Run] = '/' then // comment?
    begin
      if fLine[Run + 1] = '/' then // is end of directive as well
      begin
        fRange := rsUnknown;
        Exit;
      end
      else
        if fLine[Run + 1] = '*' then // might be embedded only
        begin
          fRange := rsDirectiveComment;
          Exit;
        end;
    end;
    if (fLine[Run] = '\') and (fLine[Run +1 ] = #0) then // a multiline directive
    begin
      Inc(Run);
      fRange := rsMultiLineDirective;
      Exit;
    end;
    Inc(Run);
  until fLine[Run] in [#0, #10, #13];
end;

procedure TSynCppSyn.DirectiveEndProc;
begin
  fTokenID := tkDirective;
  case FLine[Run] of
    #0:
      begin
        NullProc;
        Exit;
      end;
    #10:
      begin
        LFProc;
        Exit;
      end;
    #13:
      begin
        CRProc;
        Exit;
      end;
  end;
  fRange := rsUnknown;
  repeat
    case FLine[Run] of
      #0, #10, #13: Break;
      #9,#32: begin
        fSpaceRange := rsMultiLineDirective;
        fRange := rsSpace;
        Exit;
      end;
      '/': // comment?
        begin
          case fLine[Run + 1] of
            '/': // is end of directive as well
              begin
                fRange := rsUnknown;
                Exit;
              end;
            '*': // might be embedded only
              begin
                fRange := rsDirectiveComment;
                Exit;
              end;
          end;
        end;
      '\': // yet another line?
        begin
          if fLine[Run + 1] = #0 then
          begin
            Inc(Run);
            fRange := rsMultiLineDirective;
            Exit;
          end;
        end;
    end;
    Inc(Run);
  until fLine[Run] in [#0, #10, #13];
end;

procedure TSynCppSyn.EqualProc;
begin
  fTokenID := tkSymbol;
  case FLine[Run + 1] of
    '=':                               {logical equal}
      begin
        inc(Run, 2);
        FExtTokenID := xtkLogEqual;
      end;
  else                                 {assign}
    begin
      inc(Run);
      FExtTokenID := xtkAssign;
    end;
  end;
end;

procedure TSynCppSyn.GreaterProc;
begin
  fTokenID := tkSymbol;
  Case FLine[Run + 1] of
    '=':                               {greater than or equal to}
      begin
        inc(Run, 2);
        FExtTokenID := xtkGreaterThanEqual;
      end;
    '>':
      begin
        if FLine[Run + 2] = '=' then   {shift right assign}
        begin
          inc(Run, 3);
          FExtTokenID := xtkShiftRightAssign;
        end
        else                           {shift right}
        begin
          inc(Run, 2);
          FExtTokenID := xtkShiftRight;
        end;
      end;
  else                                 {greater than}
    begin
      inc(Run);
      FExtTokenID := xtkGreaterThan;
    end;
  end;
end;

procedure TSynCppSyn.QuestionProc;
begin
  fTokenID := tkSymbol;                {conditional}
  FExtTokenID := xtkQuestion;
  inc(Run);
end;

procedure TSynCppSyn.IdentProc;
begin
  fTokenID := IdentKind((fLine + Run));
  inc(Run, fStringLen);
  while Identifiers[fLine[Run]] do inc(Run);
end;

procedure TSynCppSyn.LFProc;
begin
  fTokenID := tkSpace;
  inc(Run);
end;

procedure TSynCppSyn.LowerProc;
begin
  fTokenID := tkSymbol;
  case FLine[Run + 1] of
    '=':                               {less than or equal to}
      begin
        inc(Run, 2);
        FExtTokenID := xtkLessThanEqual;
      end;
    '<':
      begin
        if FLine[Run + 2] = '=' then   {shift left assign}
        begin
          inc(Run, 3);
          FExtTokenID := xtkShiftLeftAssign;
        end
        else                           {shift left}
        begin
          inc(Run, 2);
          FExtTokenID := xtkShiftLeft;
        end;
      end;
  else                                 {less than}
    begin
      inc(Run);
      FExtTokenID := xtkLessThan;
    end;
  end;
end;

procedure TSynCppSyn.MinusProc;
begin
  fTokenID := tkSymbol;
  case FLine[Run + 1] of
    '=':                               {subtract assign}
      begin
        inc(Run, 2);
        FExtTokenID := xtkSubtractAssign;
      end;
    '-':                               {decrement}
      begin
        inc(Run, 2);
        FExtTokenID := xtkDecrement;
      end;
    '>':                               {arrow}
      begin
        inc(Run, 2);
        FExtTokenID := xtkArrow;
      end;
  else                                 {subtract}
    begin
      inc(Run);
      FExtTokenID := xtkSubtract;
    end;
  end;
end;

procedure TSynCppSyn.ModSymbolProc;
begin
  fTokenID := tkSymbol;
  case FLine[Run + 1] of
    '=':                               {mod assign}
      begin
        inc(Run, 2);
        FExtTokenID := xtkModAssign;
      end;
  else                                 {mod}
    begin
      inc(Run);
      FExtTokenID := xtkMod;
    end;
  end;
end;

procedure TSynCppSyn.NotSymbolProc;
begin
  fTokenID := tkSymbol;
  case FLine[Run + 1] of
    '=':                               {not equal}
      begin
        inc(Run, 2);
        FExtTokenID := xtkNotEqual;
      end;
  else                                 {not}
    begin
      inc(Run);
      FExtTokenID := xtkLogComplement;
    end;
  end;
end;

procedure TSynCppSyn.NullProc;
begin
  if ((Run-1>=1) and (fLine[Run-1] in [#9,#32])) and
  (fRange in [rsCppComment, rsDirective, 
    rsString, rsMultilineString, rsMultilineDirective]) then begin
    fRange := rsUnknown;
  end;
  fTokenID := tkNull;
end;

procedure TSynCppSyn.NumberProc;
var
  idx1: Integer; // token[1]
  i: Integer;
begin
  idx1 := Run;
  Inc(Run);
  fTokenID := tkNumber;
  while FLine[Run] in
    ['0'..'9', 'A'..'F', 'a'..'f', '.', 'u', 'U', 'l', 'L', 'x', 'X', '-', '+',''''] do
  begin
    case FLine[Run] of
      '''': if (fTokenID  <> tkNumber) then begin
          fTokenID := tkUnknown;
          Exit;
        end;
      '.':
        if FLine[Succ(Run)] = '.' then
          Break
        else
          if (fTokenID <> tkHex) then
            fTokenID := tkFloat
          else // invalid
          begin
            fTokenID := tkUnknown;
            Exit;
          end;
      '-', '+':
        begin
          if fTokenID <> tkFloat then // number <> float. an arithmetic operator
            Exit;
          if not (FLine[Pred(Run)] in ['e', 'E']) then
            Exit; // number = float, but no exponent. an arithmetic operator
          if not (FLine[Succ(Run)] in ['0'..'9', '+', '-']) then // invalid
          begin
            Inc(Run);
            fTokenID := tkUnknown;
            Exit;
          end
        end;
      '0'..'7':
        if (Run = Succ(idx1)) and (FLine[idx1] = '0') then // octal number
          fTokenID := tkOctal;
      '8', '9':
        if (FLine[idx1] = '0') and
           ((fTokenID <> tkHex) and (fTokenID <> tkFloat)) then // invalid octal char
             fTokenID := tkUnknown;
      'a'..'d', 'A'..'D':
        if fTokenID <> tkHex then // invalid char
          Break;
      'e', 'E':
        if (fTokenID <> tkHex) then
          if FLine[Pred(Run)] in ['0'..'9'] then // exponent
          begin
            for i := idx1 to Pred(Run) do
              if FLine[i] in ['e', 'E'] then // too many exponents
              begin
                fTokenID := tkUnknown;
                Exit;
              end;
            if not (FLine[Succ(Run)] in ['0'..'9', '+', '-']) then
              Break
            else
              fTokenID := tkFloat
          end
          else // invalid char
            Break;
      'f', 'F':
        if fTokenID <> tkHex then
        begin
          for i := idx1 to Pred(Run) do
            if FLine[i] in ['f', 'F'] then // declaration syntax error
            begin
              fTokenID := tkUnknown;
              Exit;
            end;
          if fTokenID = tkFloat then
          begin
            if fLine[Pred(Run)] in ['l', 'L'] then // can't mix
              Break;
          end
          else
            fTokenID := tkFloat;
        end;
      'l', 'L':
        begin
          for i := idx1 to Run - 2 do
            if FLine[i] in ['l', 'L'] then // declaration syntax error
            begin
              fTokenID := tkUnknown;
              Exit;
            end;
          if fTokenID = tkFloat then
            if fLine[Pred(Run)] in ['f', 'F'] then // can't mix
              Break;
        end;
      'u', 'U':
        if fTokenID = tkFloat then // not allowed
          Break
        else
          for i := idx1 to Pred(Run) do
            if FLine[i] in ['u', 'U'] then // declaration syntax error
            begin
              fTokenID := tkUnknown;
              Exit;
            end;
      'x', 'X':
        if (Run = Succ(idx1)) and   // 0x... 'x' must be second char
           (FLine[idx1] = '0') and  // 0x...
           (FLine[Succ(Run)] in ['0'..'9', 'a'..'f', 'A'..'F']) then // 0x... must be continued with a number
             fTokenID := tkHex
           else // invalid char
           begin
             if (not Identifiers[fLine[Succ(Run)]]) and
                (FLine[Succ(idx1)] in ['x', 'X']) then
             begin
               Inc(Run); // highlight 'x' too
               fTokenID := tkUnknown;
             end;
             Break;
           end;
    end; // case
    Inc(Run);
  end; // while
  {
  if (FLine[Run] in ['A'..'Z', 'a'..'z', '_']) then
    fTokenID := tkIdentifier
  }
  if (FLine[Run-1] in ['''']) then
    fTokenID := tkUnknown;
end;

procedure TSynCppSyn.OrSymbolProc;
begin
  fTokenID := tkSymbol;
  case FLine[Run + 1] of
    '=':                               {or assign}
      begin
        inc(Run, 2);
        FExtTokenID := xtkIncOrAssign;
      end;
    '|':                               {logical or}
      begin
        inc(Run, 2);
        FExtTokenID := xtkLogOr;
      end;
  else                                 {or}
    begin
      inc(Run);
      FExtTokenID := xtkIncOr;
    end;
  end;
end;

procedure TSynCppSyn.PlusProc;
begin
  fTokenID := tkSymbol;
  case FLine[Run + 1] of
    '=':                               {add assign}
      begin
        inc(Run, 2);
        FExtTokenID := xtkAddAssign;
      end;
    '+':                               {increment}
      begin
        inc(Run, 2);
        FExtTokenID := xtkIncrement;
      end;
  else                                 {add}
    begin
      inc(Run);
      FExtTokenID := xtkAdd;
    end;
  end;
end;

procedure TSynCppSyn.PointProc;
begin
  fTokenID := tkSymbol;
  if (FLine[Run + 1] = '.') and (FLine[Run + 2] = '.') then
    begin                              {ellipse}
      inc(Run, 3);
      FExtTokenID := xtkEllipse;
    end
  else
    if FLine[Run + 1] in ['0'..'9'] then // float
    begin
      Dec(Run); // numberproc must see the point
      NumberProc;
    end
  else                                 {point}
    begin
      inc(Run);
      FExtTokenID := xtkPoint;
    end;
end;

procedure TSynCppSyn.RoundCloseProc;
begin
  inc(Run);
  fTokenID := tkSymbol;
  FExtTokenID := xtkRoundClose;
  dec(fParenthesisLevel);
end;

procedure TSynCppSyn.RoundOpenProc;
begin
  inc(Run);
  FTokenID := tkSymbol;
  FExtTokenID := xtkRoundOpen;
  inc(fParenthesisLevel);
end;

procedure TSynCppSyn.SemiColonProc;
begin
  inc(Run);
  fTokenID := tkSymbol;
  FExtTokenID := xtkSemiColon;
  if fRange = rsAsm then fRange := rsUnknown;
end;

procedure TSynCppSyn.SlashProc;
begin
  case FLine[Run + 1] of
    '/':                               {c++ style comments}
      begin
        fTokenID := tkComment;
        inc(Run,2);
        fRange := rsCppComment;
        Exit;
        {
        while fLine[Run] <> #0 do begin
          Inc(Run);
        end;
        if fLine[Run-1] = '\' then
          fRange := rsCppComment; // multiline C++ comment
        }
      end;
    '*':                               {c style comments}
      begin
        fTokenID := tkComment;
        if fRange = rsAsm then
          fRange := rsAnsiCAsm
        else if fRange = rsAsmBlock then
          fRange := rsAnsiCAsmBlock
        else if fRange = rsDirective then
          fRange := rsDirectiveComment
        else
          fRange:=rsAnsiC;
        inc(Run, 2);
        if FLine[Run] <> #0 then
          AnsiCProc;
      end;
    '=':                               {divide assign}
      begin
        inc(Run, 2);
        fTokenID := tkSymbol;
        FExtTokenID := xtkDivideAssign;
      end;
  else                                 {divide}
    begin
      inc(Run);
      fTokenID := tkSymbol;
      FExtTokenID := xtkDivide;
    end;
  end;
end;

procedure TSynCppSyn.SpaceProc;
begin
  inc(Run);
  fTokenID := tkSpace;
  while FLine[Run] in [#1..#9, #11, #12, #14..#32] do inc(Run);
  fRange := fSpaceRange;
  fSpaceRange := rsUnknown;

  {
  if (fRange in [rsCppComment, rsMultiLineDirective,
    rsString]) and (FLine[Run]=#0) then begin
    fRange := rsUnknown;
  end;
  }
end;

procedure TSynCppSyn.SquareCloseProc;
begin
  inc(Run);
  fTokenID := tkSymbol;
  FExtTokenID := xtkSquareClose;
  dec(fBracketLevel);
end;

procedure TSynCppSyn.SquareOpenProc;
begin
  inc(Run);
  fTokenID := tkSymbol;
  FExtTokenID := xtkSquareOpen;
  inc(fBracketLevel);
end;

procedure TSynCppSyn.StarProc;
begin
  fTokenID := tkSymbol;
  case FLine[Run + 1] of
    '=':                               {multiply assign}
      begin
        inc(Run, 2);
        FExtTokenID := xtkMultiplyAssign;
      end;
  else                                 {star}
    begin
      inc(Run);
      FExtTokenID := xtkStar;
    end;
  end;
end;

procedure TSynCppSyn.RawStringProc;
var
  NoEscaping : boolean;
begin
  NoEscaping := False;
  if fRange = rsRawStringNotEscaping then
    NoEscaping := True;
  fTokenID := tkRawString;
  fRange := rsRawString;

  while not (
    (fLine[Run] in [#0, #10, #13]) or ((not NoEscaping) and (fLine[Run] = '"'))
    ) do begin
    if fLine[Run] = '(' then begin
      NoEscaping:=True;
    end else if fLine[Run] = ')' then begin
      NoEscaping:=False;
    end else if fLine[Run] in [#32,#9] then begin
      fRange := rsSpace;
      if NoEscaping then
        fSpaceRange:= rsRawStringNotEscaping
      else
        fSpaceRange:= rsRawStringEscaping;
      Exit;
    end;
    inc(Run);
  end;
  if (FLine[Run] = #34) then begin
    inc(Run);
  end;
  fRange := rsUnknown;  
end;

procedure TSynCppSyn.StringEscapeSeqProc;
var
  i:integer;
begin
  fTokenID := tkStringEscapeSeq;

  inc(Run);
  case fLine[Run] of
    '''','"','?','a','b','f','n','r','t','v','\': begin
        inc(Run);
      end;
    '0'..'7': begin
        for i:=1 to 3 do begin
          if not (fLine[Run] in ['0'..'7']) then
            break;
          inc(Run);
        end;
      end;
    '8'..'9': begin
        fTokenID := tkUnknown;
        inc(Run);
      end;
    'x': begin
        inc(Run);
        if not (fLine[Run] in ['0'..'9','a'..'f','A'..'F'] ) then begin
          fTokenID := tkUnknown;
        end else begin
          while (fLine[Run] in ['0'..'9','a'..'f','A'..'F']) do
            inc(Run);
        end;
      end;
    'u': begin
        inc(Run);
        for i:=1 to 4 do begin
          if not (fLine[Run] in ['0'..'7']) then begin
            fTokenID := tkUnknown;
            break;
          end;
          inc(Run);
        end;
      end;
    'U': begin
        inc(Run);
        for i:=1 to 8 do begin
          if not (fLine[Run] in ['0'..'7']) then begin
            fTokenID := tkUnknown;
            break;
          end;
          inc(Run);
        end;
      end;
  end;
  if fRange = rsMultilineStringEscapeSeq then
    fRange:=rsMultilineString
  else
    fRange:=rsString;
end;

procedure TSynCppSyn.StringStartProc;
begin
  fTokenID := tkString;
  inc(Run);
  if fLine[Run]=#0 then begin
    fRange := rsUnknown;
    Exit;
  end;
  StringProc;
end;

procedure TSynCppSyn.StringProc;
begin
  if fLine[Run]=#0 then begin
    fRange:=rsUnknown;
    Exit;
  end;
  fTokenID := tkString;
  fRange := rsString;
  while not (fLine[Run] in [#0, #10, #13, #34]) do begin
    if (fLine[Run] in [#9,#32]) then begin
      fSpaceRange := rsString;
      fRange := rsSpace;
      Exit;
    end;
    if fLine[Run] = '\' then begin
      case fLine[Run + 1] of
        '''','"','\','?','a','b','f','n','r','t','v','0'..'9','x','u','U':
          begin
            fRange := rsStringEscapeSeq;
            Exit;
          end;
        #00:
          begin
            Inc(Run);
            fRange := rsMultilineString;
            Exit;
          end;
      end;
    end;
    inc(Run);
  end;
  if FLine[Run] = #34 then begin
    inc(Run);
  end;
  fRange := rsUnknown;
  {end;}
end;

procedure TSynCppSyn.StringEndProc;
begin
  fTokenID := tkString;
  case FLine[Run] of
    #0:
      begin
        NullProc;
        Exit;
      end;
    #10:
      begin
        LFProc;
        Exit;
      end;
    #13:
      begin
        CRProc;
        Exit;
      end;
  end;

  fRange := rsUnknown;

  while not (fLine[Run] in [#0, #10, #13, #34]) do begin
    if (fLine[Run] in [#9,#32]) then begin
      fSpaceRange := rsMultilineString;
      fRange := rsSpace;
      Exit;
    end;
    case FLine[Run] of
      #0, #10, #13: Break;
      '\':
        begin
          case fLine[Run + 1] of
            '''','"','\','?','a','b','f','n','r','t','v','0'..'9','x','u','U':
              begin
                fRange := rsMultilineStringEscapeSeq;
                Exit;
              end;
            #00:
              begin
                Inc(Run);
                fRange := rsMultilineString;
                Exit;
              end;
          end;
        end;
      #34: Break;
    end;
    inc(Run);
  end;
  if FLine[Run] = #34 then
    inc(Run);
end;

procedure TSynCppSyn.TildeProc;
begin
  inc(Run);                            {bitwise complement}
  fTokenId := tkSymbol;
  FExtTokenID := xtkBitComplement;
end;

procedure TSynCppSyn.XOrSymbolProc;
begin
  fTokenID := tkSymbol;
  Case FLine[Run + 1] of
  	'=':                               {xor assign}
      begin
        inc(Run, 2);
        FExtTokenID := xtkXorAssign;
      end;
  else                                 {xor}
    begin
      inc(Run);
      FExtTokenID := xtkXor;
    end;
  end;
end;

procedure TSynCppSyn.UnknownProc;
begin
{$IFDEF SYN_MBCSSUPPORT}
  if FLine[Run] in LeadBytes then
    Inc(Run, 2)
  else
{$ENDIF}
  Inc(Run);
  fTokenID := tkUnknown;
end;

procedure TSynCppSyn.Next;
begin
  fAsmStart := False;
  fTokenPos := Run;
  repeat
    case fRange of
      rsAnsiC, rsAnsiCAsm,
      rsAnsiCAsmBlock, rsDirectiveComment: AnsiCProc;
      rsString: StringProc;
      rsCppComment: AnsiCppProc;
      rsMultiLineDirective: DirectiveEndProc;
      rsMultilineString: StringEndProc;
      rsSpace: SpaceProc;
      rsRawStringEscaping, rsRawStringNotEscaping: RawStringProc;
      rsStringEscapeSeq, rsMultilineStringEscapeSeq : StringEscapeSeqProc;
      rsChar: begin
        if fLine[Run]='''' then begin
          fRange := rsUnknown;
          fTokenID := tkChar;
          inc(Run);
        end else
          AsciiCharProc;
        end;
      else begin
        fRange := rsUnknown;
        if (fLine[Run]='R') and (fLine[Run+1]='"') then begin
          inc(Run,2);
          RawStringProc;
        end else if (fLine[Run] in ['L','u','U'])  and (fLine[Run+1]='"') then begin
          inc(Run,1);
          StringStartProc;
        end else if (fLine[Run] = 'u') and (fLine[Run+1]='8') and (fLine[Run+2]='"') then begin
          inc(Run,2);
          StringStartProc;
        end else
          fProcTable[fLine[Run]];
      end;
    end;
  until (fTokenID = tkNull) or (Run > fTokenPos);
end;

function TSynCppSyn.GetDefaultAttribute(Index: integer): TSynHighlighterAttributes;
begin
  case Index of
    SYN_ATTR_COMMENT: Result := fCommentAttri;
    SYN_ATTR_IDENTIFIER: Result := fIdentifierAttri;
    SYN_ATTR_KEYWORD: Result := fKeyAttri;
    SYN_ATTR_STRING: Result := fStringAttri;
    SYN_ATTR_WHITESPACE: Result := fSpaceAttri;
    SYN_ATTR_SYMBOL: Result := fSymbolAttri;
  else
    Result := nil;
  end;
end;

function TSynCppSyn.GetEol: Boolean;
begin
  Result := (fTokenID = tkNull);
end;

function TSynCppSyn.GetRange: Pointer;
begin
  Result := Pointer(fRange);
end;

function TSynCppSyn.GetParenthesisLevel: integer;
begin
  Result := fParenthesisLevel;
end;

function TSynCppSyn.GetBracketLevel: integer;
begin
  Result := fBracketLevel;
end;

function TSynCppSyn.GetBraceLevel: integer;
begin
  Result := fBraceLevel;
end;

function TSynCppSyn.GetToken: String;
var
  Len: LongInt;
begin
  Len := Run - fTokenPos;
  SetString(Result, (FLine + fTokenPos), Len);
end;

function TSynCppSyn.GetTokenFinished: boolean;
begin
  case fTokenId of
    tkComment:
      Result := (fRange = rsUnknown);
      {
      Result := not (fRange  in [rsAnsiC, rsAnsiCAsm,
        rsAnsiCAsmBlock, rsDirectiveComment,rsCppComment]);
      }
    tkString:
      Result := (fRange = rsUnknown);
    tkRawString:
      Result := (fRange = rsUnknown);
  else
    Result := False;
  end;
end;

function TSynCppSyn.GetTokenID: TtkTokenKind;
begin
  Result := fTokenId;
  if ((fRange = rsAsm) or (fRange = rsAsmBlock)) and not fAsmStart
    and not (fTokenId in [tkComment, tkSpace, tkNull])
  then
    Result := tkAsm;
end;

function TSynCppSyn.GetExtTokenID: TxtkTokenKind;
begin
  Result := FExtTokenID;
end;

function TSynCppSyn.GetTokenType: TSynHighlighterTokenType;
begin
  case fTokenID of
    tkComment: Result := httComment;
    tkDirective: Result := httPreprocessDirective;
    tkIdentifier: Result := httIdentifier;
    tkKey: Result := httKeyword;
    tkSpace: begin
      case fRange of
        rsAnsiC, rsAnsiCAsm, rsAnsiCAsmBlock,
          rsAsm, rsAsmBlock, rsDirectiveComment,rsCppComment :
          Result := httComment;
        rsDirective, rsMultiLineDirective:
          Result := httPreprocessDirective;
        rsString, rsMultiLineString, rsStringEscapeSeq,
          rsMultiLineStringEscapeSeq,
          rsRawString:
          Result := httString;
        rsChar :
          Result := httCharacter;
      else
        Result := httSpace;
      end;
    end;
    tkString: Result := httString;
    tkStringEscapeSeq: Result := httStringEscapeSequence;
    tkRawString: Result := httString;
    tkChar: Result := httCharacter;
    tkSymbol: Result := httSymbol;
    else Result := httDefault;
  end;
end;

function TSynCppSyn.GetTokenAttribute: TSynHighlighterAttributes;
begin
  case fTokenID of
    tkAsm: Result := fAsmAttri;
    tkComment: Result := fCommentAttri;
    tkDirective: Result := fDirecAttri;
    tkIdentifier: Result := fIdentifierAttri;
    tkKey: Result := fKeyAttri;
    tkNumber: Result := fNumberAttri;
    tkFloat: Result := fFloatAttri;
    tkHex: Result := fHexAttri;
    tkOctal: Result := fOctalAttri;
    tkSpace: Result := fSpaceAttri;
    tkString: Result := fStringAttri;
    tkStringEscapeSeq: Result := fStringEscapeSeqAttri;
    tkRawString: Result := fStringAttri;
    tkChar: Result := fCharAttri;
    tkSymbol: Result := fSymbolAttri;
    tkUnknown: Result := fInvalidAttri;
    else Result := nil;
  end;
end;

function TSynCppSyn.GetTokenKind: integer;
begin
  Result := Ord(GetTokenID);
end;

function TSynCppSyn.GetTokenPos: Integer;
begin
  Result := fTokenPos;
end;

procedure TSynCppSyn.ResetRange;
begin
  fRange:= rsUnknown;
  fSpaceRange := rsUnknown;
end;

procedure TSynCppSyn.ResetParenthesisLevel;
begin
  fParenthesisLevel := 0;
end;

procedure TSynCppSyn.ResetBracketLevel;
begin
  fBracketLevel := 0;
end;

procedure TSynCppSyn.ResetBraceLevel;
begin
  fBraceLevel := 0;
end;

procedure TSynCppSyn.SetRange(Value: Pointer);
begin
  fRange := TRangeState(Value);
end;

procedure TSynCppSyn.SetParenthesisLevel(Value: integer);
begin
  fParenthesisLevel := Value;
end;

procedure TSynCppSyn.SetBracketLevel(Value: integer);
begin
  fBracketLevel := Value;
end;

procedure TSynCppSyn.SetBraceLevel(Value: integer);
begin
  fBraceLevel := Value;
end;


function TSynCppSyn.GetIsLastLineCommentNotFinish(Value:Pointer):boolean;
var
  range: TRangeState;
begin
  range := TRangeState(Value);
  Result:= (range in [rsAnsiC, rsAnsiCAsm, 
    rsAnsiCAsmBlock, rsDirectiveComment, rsCppComment]);
end;

function TSynCppSyn.GetIsLastLineStringNotFinish(Value:Pointer):boolean;
var
  range: TRangeState;
begin
  range := TRangeState(Value);
  Result:= (range in [rsMultiLineString]);
end;

procedure TSynCppSyn.EnumUserSettings(settings: TStrings);
begin
  { returns the user settings that exist in the registry }
  with TBetterRegistry.Create do
  begin
    try
      RootKey := HKEY_LOCAL_MACHINE;
      if OpenKeyReadOnly('\SOFTWARE\Borland\C++Builder') then
      begin
        try
          GetKeyNames(settings);
        finally
          CloseKey;
        end;
      end;
    finally
      Free;
    end;
  end;
end;

function TSynCppSyn.UseUserSettings(settingIndex: integer): boolean;
// Possible parameter values:
//   index into TStrings returned by EnumUserSettings
// Possible return values:
//   true : settings were read and used
//   false: problem reading settings or invalid version specified - old settings
//          were preserved

  function ReadCPPBSettings(settingIndex: integer): boolean;

    function ReadCPPBSetting(settingTag: string; attri: TSynHighlighterAttributes; key: string): boolean;

      function ReadCPPB1(settingTag: string; attri: TSynHighlighterAttributes; name: string): boolean;
      var
        i: integer;
      begin
        for i := 1 to Length(name) do
          if name[i] = ' ' then name[i] := '_';
        Result := attri.LoadFromBorlandRegistry(HKEY_CURRENT_USER,
             '\SOFTWARE\Borland\C++Builder\'+settingTag+'\Highlight',name,true);
      end; { ReadCPPB1 }

      function ReadCPPB3OrMore(settingTag: string; attri: TSynHighlighterAttributes; key: string): boolean;
      begin
        Result := attri.LoadFromBorlandRegistry(HKEY_CURRENT_USER,
                 '\Software\Borland\C++Builder\'+settingTag+'\Editor\Highlight',
                 key,false);
      end; { ReadCPPB3OrMore }

    begin { ReadCPPBSetting }
      try
        if (settingTag[1] = '1')
          then Result := ReadCPPB1(settingTag,attri,key)
          else Result := ReadCPPB3OrMore(settingTag,attri,key);
      except Result := false; end;
    end; { ReadCPPBSetting }

  var
    tmpStringAttri    : TSynHighlighterAttributes;
    tmpCharAttri      : TSynHighlighterAttributes;
    tmpNumberAttri    : TSynHighlighterAttributes;
    tmpFloatAttri     : TSynHighlighterAttributes;
    tmpHexAttri       : TSynHighlighterAttributes;
    tmpOctalAttri     : TSynHighlighterAttributes;
    tmpKeyAttri       : TSynHighlighterAttributes;
    tmpSymbolAttri    : TSynHighlighterAttributes;
    tmpAsmAttri       : TSynHighlighterAttributes;
    tmpCommentAttri   : TSynHighlighterAttributes;
    tmpIdentifierAttri: TSynHighlighterAttributes;
    tmpInvalidAttri   : TSynHighlighterAttributes;
    tmpSpaceAttri     : TSynHighlighterAttributes;
    tmpDirecAttri     : TSynHighlighterAttributes;
    s                 : TStringList;

  begin { ReadCPPBSettings }
    s := TStringList.Create;
    try
      EnumUserSettings(s);
      if settingIndex >= s.Count then Result := false
      else begin
        tmpStringAttri    := TSynHighlighterAttributes.Create('');
        tmpCharAttri      := TSynHighlighterAttributes.Create('');
        tmpNumberAttri    := TSynHighlighterAttributes.Create('');
        tmpFloatAttri     := TSynHighlighterAttributes.Create('');
        tmpHexAttri       := TSynHighlighterAttributes.Create('');
        tmpOctalAttri     := TSynHighlighterAttributes.Create('');
        tmpKeyAttri       := TSynHighlighterAttributes.Create('');
        tmpSymbolAttri    := TSynHighlighterAttributes.Create('');
        tmpAsmAttri       := TSynHighlighterAttributes.Create('');
        tmpCommentAttri   := TSynHighlighterAttributes.Create('');
        tmpIdentifierAttri:= TSynHighlighterAttributes.Create('');
        tmpInvalidAttri   := TSynHighlighterAttributes.Create('');
        tmpSpaceAttri     := TSynHighlighterAttributes.Create('');
        tmpDirecAttri     := TSynHighlighterAttributes.Create('');
        tmpStringAttri    .Assign(fStringAttri);
        tmpCharAttri      .Assign(fCharAttri);
        tmpNumberAttri    .Assign(fNumberAttri);
        tmpFloatAttri     .Assign(fFloatAttri);
        tmpHexAttri       .Assign(fHexAttri);
        tmpOctalAttri     .Assign(fOctalAttri);
        tmpKeyAttri       .Assign(fKeyAttri);
        tmpSymbolAttri    .Assign(fSymbolAttri);
        tmpAsmAttri       .Assign(fAsmAttri);
        tmpCommentAttri   .Assign(fCommentAttri);
        tmpIdentifierAttri.Assign(fIdentifierAttri);
        tmpInvalidAttri   .Assign(fInvalidAttri);
        tmpSpaceAttri     .Assign(fSpaceAttri);
        tmpDirecAttri     .Assign(fDirecAttri);
        if s[settingIndex][1] = '1'
          then Result := ReadCPPBSetting(s[settingIndex],fAsmAttri,'Plain text')
          else Result := ReadCPPBSetting(s[settingIndex],fAsmAttri,'Assembler');
        Result := Result                                                         and
                  ReadCPPBSetting(s[settingIndex],fCommentAttri,'Comment')       and
                  ReadCPPBSetting(s[settingIndex],fIdentifierAttri,'Identifier') and
                  ReadCPPBSetting(s[settingIndex],fInvalidAttri,'Illegal Char')  and
                  ReadCPPBSetting(s[settingIndex],fKeyAttri,'Reserved word')     and
                  ReadCPPBSetting(s[settingIndex],fNumberAttri,'Integer')        and
                  ReadCPPBSetting(s[settingIndex],fFloatAttri,'Float')           and
                  ReadCPPBSetting(s[settingIndex],fHexAttri,'Hex')               and
                  ReadCPPBSetting(s[settingIndex],fOctalAttri,'Octal')           and
                  ReadCPPBSetting(s[settingIndex],fSpaceAttri,'Whitespace')      and
                  ReadCPPBSetting(s[settingIndex],fStringAttri,'String')         and
                  ReadCPPBSetting(s[settingIndex],fCharAttri,'Character')             and
                  ReadCPPBSetting(s[settingIndex],fSymbolAttri,'Symbol')         and
                  ReadCPPBSetting(s[settingIndex],fDirecAttri,'Preprocessor');
        if not Result then begin
          fStringAttri    .Assign(tmpStringAttri);
          fCharAttri      .Assign(tmpCharAttri);
          fNumberAttri    .Assign(tmpNumberAttri);
          fFloatAttri     .Assign(tmpFloatAttri);
          fHexAttri       .Assign(tmpHexAttri);
          fOctalAttri     .Assign(tmpOctalAttri);
          fKeyAttri       .Assign(tmpKeyAttri);
          fSymbolAttri    .Assign(tmpSymbolAttri);
          fAsmAttri       .Assign(tmpAsmAttri);
          fCommentAttri   .Assign(tmpCommentAttri);
          fIdentifierAttri.Assign(tmpIdentifierAttri);
          fInvalidAttri   .Assign(tmpInvalidAttri);
          fSpaceAttri     .Assign(tmpSpaceAttri);
          fDirecAttri     .Assign(tmpDirecAttri);
        end;
        tmpStringAttri    .Free;
        tmpCharAttri      .Free;
        tmpNumberAttri    .Free;
        tmpFloatAttri     .Free;
        tmpHexAttri       .Free;
        tmpOctalAttri     .Free;
        tmpKeyAttri       .Free;
        tmpSymbolAttri    .Free;
        tmpAsmAttri       .Free;
        tmpCommentAttri   .Free;
        tmpIdentifierAttri.Free;
        tmpInvalidAttri   .Free;
        tmpSpaceAttri     .Free;
        tmpDirecAttri     .Free;
      end;
    finally s.Free; end;
  end; { ReadCPPBSettings }

begin
  Result := ReadCPPBSettings(settingIndex);
end; { TSynCppSyn.UseUserSettings }

function TSynCppSyn.GetIdentChars: TSynIdentChars;
begin
  Result := TSynValidStringChars;
end;

function TSynCppSyn.IsFilterStored: Boolean;
begin
  Result := fDefaultFilter <> SYNS_FilterCPP;
end;

class function TSynCppSyn.GetLanguageName: string;
begin
  Result := SYNS_LangCPP;
end;

class function TSynCppSyn.GetCapabilities: TSynHighlighterCapabilities;
begin
  Result := inherited GetCapabilities + [hcUserSettings];
end;

function TSynCppSyn.GetSampleSource: string;
begin
  Result := '// Syntax Highlighting'#13#10+
            'void __fastcall TForm1::Button1Click(TObject *Sender)'#13#10+
            '{'#13#10+
            '  int number = 123456;'#13#10+
            '  char c = ''a'';'#13#10+
            '  Caption = "The number is " + IntToStr(i);'#13#10+
            '  for (int i = 0; i <= number; i++)'#13#10+
            '  {'#13#10+
            '    x -= 0xff;'#13#10+
            '    x -= 023;'#13#10+
            '    x += 1.0;'#13#10+
            '    x += @; /* illegal character */'#13#10+
            '  }'#13#10+
            '  #ifdef USE_ASM'#13#10+
            '    asm'#13#10+
            '    {'#13#10+
            '      ASM MOV AX, 0x1234'#13#10+
            '      ASM MOV i, AX'#13#10+
            '    }'#13#10+
            '  #endif'#13#10+
            '}';

end;

initialization
begin
  MakeIdentTable;
{$IFNDEF SYN_CPPB_1}
  RegisterPlaceableHighlighter(TSynCppSyn);
{$ENDIF}

  CppKeywords := TStringHash.Create();

  CppKeywords.Add('and',1);
  CppKeywords.Add('and_eq',1);
  CppKeywords.Add('bitand',1);
  CppKeywords.Add('bitor',1);
  CppKeywords.Add('break',1);
  CppKeywords.Add('compl',1);
  CppKeywords.Add('constexpr',1);
  CppKeywords.Add('const_cast',1);
  CppKeywords.Add('continue',1);
  CppKeywords.Add('dynamic_cast',1);
  CppKeywords.Add('else',1);
  CppKeywords.Add('explicit',1);
  CppKeywords.Add('export',1);
  CppKeywords.Add('extern',1);
  CppKeywords.Add('false',1);
  CppKeywords.Add('for',1);
  CppKeywords.Add('mutable',1);
  CppKeywords.Add('noexcept',1);
  CppKeywords.Add('not',1);
  CppKeywords.Add('not_eq',1);
  CppKeywords.Add('nullptr',1);
  CppKeywords.Add('or',1);
  CppKeywords.Add('or_eq',1);
  CppKeywords.Add('register',1);
  CppKeywords.Add('reinterpret_cast',1);
  CppKeywords.Add('static_assert',1);
  CppKeywords.Add('static_cast',1);
  CppKeywords.Add('template',1);
  CppKeywords.Add('this',1);
  CppKeywords.Add('thread_local',1);
  CppKeywords.Add('true',1);
  CppKeywords.Add('typename',1);
  CppKeywords.Add('virtual',1);
  CppKeywords.Add('volatile',1);
  CppKeywords.Add('xor',1);
  CppKeywords.Add('xor_eq',1);
  CppKeywords.Add('delete',1);
  CppKeywords.Add('delete[]',1);
  CppKeywords.Add('goto',1);
  CppKeywords.Add('new',1);
  CppKeywords.Add('return',1);
  CppKeywords.Add('throw',1);
  CppKeywords.Add('using',1);
  CppKeywords.Add('case',1);
  CppKeywords.Add('default',1);

  CppKeywords.Add('alignas',1); 
  CppKeywords.Add('alignof',1); 
  CppKeywords.Add('decltype',1);
  CppKeywords.Add('if',1);
  CppKeywords.Add('sizeof',1);
  CppKeywords.Add('switch',1);
  CppKeywords.Add('typeid',1);
  CppKeywords.Add('while',1);

  CppKeywords.Add('asm',1);
  CppKeywords.Add('catch',1);
  CppKeywords.Add('do',1);
  CppKeywords.Add('namespace',1); 
  CppKeywords.Add('try',1);

  CppKeywords.Add('atomic_cancel',1);
  CppKeywords.Add('atomic_commit',1);
  CppKeywords.Add('atomic_noexcept',1);
  CppKeywords.Add('concept',1);
  CppKeywords.Add('consteval',1);
  CppKeywords.Add('constinit',1);
  CppKeywords.Add('co_wait',1);
  CppKeywords.Add('co_return',1);
  CppKeywords.Add('co_yield',1);
  CppKeywords.Add('reflexpr',1);
  CppKeywords.Add('requires',1);

  CppKeywords.Add('auto',1);
  CppKeywords.Add('bool',1);
  CppKeywords.Add('char',1);
  CppKeywords.Add('char8_t',1);
  CppKeywords.Add('char16_t',1);
  CppKeywords.Add('char32_t',1);
  CppKeywords.Add('double',1);
  CppKeywords.Add('float',1);
  CppKeywords.Add('int',1);
  CppKeywords.Add('long',1);
  CppKeywords.Add('short',1);
  CppKeywords.Add('signed',1);
  CppKeywords.Add('unsigned',1);
  CppKeywords.Add('void',1);
  CppKeywords.Add('wchar_t',1);

  CppKeywords.Add('const',1);
  CppKeywords.Add('inline',1);

  CppKeywords.Add('class',1);
  CppKeywords.Add('enum',1);
  CppKeywords.Add('friend',1);
  CppKeywords.Add('operator',1);
  CppKeywords.Add('private',1);
  CppKeywords.Add('protected',1);
  CppKeywords.Add('public',1);
  CppKeywords.Add('static',1);
  CppKeywords.Add('struct',1);
  CppKeywords.Add('typedef',1);
  CppKeywords.Add('union',1);

  CppKeywords.Add('nullptr',1);

end;

finalization
begin
  CppKeywords.Clear;
  CppKeywords.Free;
end;

end.

